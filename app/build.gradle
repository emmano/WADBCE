buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.2'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.+'
        classpath 'org.jesko.robolectric:robolectric-androidstudio-plugin:1.1.3'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'robolectric'



repositories {
        mavenCentral()
        jcenter()
}

configurations {
    all*.exclude group: 'org.json'
    all*.exclude group: 'asm', module: 'asm', version: '3.1'
    all*.exclude group: 'stax'
    all*.exclude group: 'xpp3'
}

android {
    compileSdkVersion 19
    buildToolsVersion "20.0.0"

    defaultConfig {
        applicationId "me.emmano.wadbce"
        minSdkVersion 11
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    lintOptions {
        abortOnError false
    }

    sourceSets {
        androidTest.java.srcDirs = ['src/androidTest/java', 'src/androidTestRobotium/java']
        androidTest.assets.srcDirs = ['src/androidTest/assets', 'src/androidTestRobotium/assets']
    }
}

dependencies {
    compile('org.roboguice:roboguice:2.0') {
        exclude group: 'aopalliance'
        exclude group: 'org.sonatype.sisu.inject'
    }
    androidTestCompile('com.google.dexmaker:dexmaker-mockito:1.0') {
        exclude group: 'com.google.dexmaker', module: 'dexmaker'
    }
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.2.1'
    androidTestCompile 'com.jeskeshouse:injected-test-runner:1.0'

    robolectricCompile 'junit:junit:4.10'
    robolectricCompile 'org.mockito:mockito-all:1.9.0'
    robolectricCompile 'com.google.android:android:4.1.1.4'
    robolectricCompile('org.roboguice:roboguice:2.0') {
        exclude group: 'aopalliance'
        exclude group: 'org.sonatype.sisu.inject'
    }

    androidTestProvided 'junit:junit:4.10'
    androidTestProvided('org.robolectric:robolectric:2.3') {
        exclude group: 'com.google.android'
    }
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
robolectric {
    imlFile 'app.iml'
    dotIdeaDir '../.idea'
    inProcessBuilds = false
}

tasks.withType(Test) {
    scanForTestClasses = false
    include "**/*Test.class"
}

gradle.projectsEvaluated {
    generateDebugSources.doLast {
        tasks['addRobolectricTestSourcesToIml'].execute()
    }
}
